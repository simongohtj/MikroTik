# Google Domain DDNS Update Script
# Tested with RouterOS 6.48.2
# Date: 22/07/2021
# Policies required: Don't require Permissions, read, write, policy, test
# https://forum.mikrotik.com/viewtopic.php?t=93458

# Create scheduled running
# /system scheduler add name=update-ddns interval=1m on-event=googledomain-ddns

# Fill in domain/subdomains, username and password below
# Username and password are the long cryptic ones Google Domains provide, not your regular Google account ones
:local domains { \
{ "domain.com";      "username_for_domain.com";      "password_for_domain.com" }; \
{ "sub.domain.com";  "username_for_sub.domain.com";  "password_for_sub.domain.com" }};

:log info ("[Google Domain] Script starting...")

# Get current WAN IP
:local currentIP [/ip cloud get public-address]

:foreach domain in=$domains do={

  :local domainName [:pick $domain 0]
  :local domainIP [:resolve "$domainName"]  
  :local userName [:pick $domain 1]
  :local password [:pick $domain 2]

  :if ($currentIP != $domainIP) do={
    :do {
      :log info ("[Google Domain] Updating $domainName from $domainIP to $currentIP")
      :local result [/tool fetch url="https://$userName:$password@domains.google.com/nic/update?hostname=$domainName&myip=$currentIP" mode=https as-value output=user]
      :if ($result->"status" = "finished") do={
        :log info ("[Google Domain] Result: " $result->"data")
      } else={
        :log info ("[Google Domain] Error updating IP " $result->"data")
      }
    } on-error={ 
      :log error ("[Google Domain] Failed to set new IP address") 
    } 
  } else={
    :log info ("[Google Domain] IP is already updated for $domainName ($domainIP)")
  }
}

:log info ("[Google Domain] Script ended")


#result of already updated: nochg 111.222.333.444
#result of successful update: good 111.222.333.444
